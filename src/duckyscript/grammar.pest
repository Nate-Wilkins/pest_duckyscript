// // // // // // // // // // // // // // // // // // // // // // // // // // // // // // // //
//
// DuckyScript
//
// Documentation: https://docs.hak5.org/hak5-usb-rubber-ducky/duckyscript-tm-quick-reference
//
// // // // // // // // // // // // // // // // // // // // // // // // // // // // // // // //

document = {
    SOI
  ~ (whitespace | NEWLINE)*
  ~ list_statement
  ~ (whitespace | NEWLINE)*
  ~ EOI
}

// // // // // // // // // // // // // // // // // // // // // // // // // // // // // // // //
//                                     Statements                                            //
// // // // // // // // // // // // // // // // // // // // // // // // // // // // // // // //
// {
list_statement = _{
  (statement | NEWLINE)*
}

statement = _{
    statement_command
  | statement_variable_declaration
  | statement_variable_assignment
  | statement_block_if
  | statement_block_while
}
// }


// // // // // // // // // // // // // // // // // // // // // // // // // // // // // // // //
//                                    Commands                                               //
// // // // // // // // // // // // // // // // // // // // // // // // // // // // // // // //
// {
statement_command = _{
    statement_command_rem
  | statement_command_defaultdelay
  | statement_command_delay
  | statement_command_stringln
  | statement_command_stringln
  | statement_command_string
  | statement_command_key
  | statement_command_inject_mod
  | statement_command_wait_for_button_press
  | statement_command_button_def
  | statement_command_disable_button
  | statement_command_enable_button
  | statement_command_led_off
  | statement_command_led_r
  | statement_command_led_g
  | statement_command_save_attackmode
  | statement_command_restore_attackmode
  | statement_command_attackmode
  | statement_command_define
  | statement_command_random_lowercase_letter
  | statement_command_random_uppercase_letter
  | statement_command_random_letter
  | statement_command_random_number
  | statement_command_random_special
  | statement_command_random_char
  | statement_command_vid_random
  | statement_command_pid_random
  | statement_command_man_random
  | statement_command_prod_random
  | statement_command_serial_random
  | statement_command_hold
  | statement_command_release
  | statement_command_restart_payload
  | statement_command_stop_payload
  | statement_command_reset
  | statement_command_hide_payload
  | statement_command_restore_payload
  | statement_command_wait_for_caps_on
  | statement_command_wait_for_caps_off
  | statement_command_wait_for_caps_change
  | statement_command_wait_for_num_on
  | statement_command_wait_for_num_off
  | statement_command_wait_for_num_change
  | statement_command_wait_for_scroll_on
  | statement_command_wait_for_scroll_off
  | statement_command_wait_for_scroll_change
  | statement_command_save_host_keyboard_lock_state
  | statement_command_restore_host_keyboard_lock_state
  | statement_command_exfil
}

// Comments.
statement_command_rem = {
    "REM"
  ~ " "?
  ~ value_any?
}
// Delays.
statement_command_defaultdelay = {
    "DEFAULTDELAY"
  ~ whitespace+
  ~ (expression | value_number)
}
statement_command_delay = {
    "DELAY"
  ~ whitespace*
  ~ (expression | value_number)?
}
// Keystroke Injection.
statement_command_stringln = {
    "STRINGLN"
  ~ " "?
  ~ (expression | value_any)?
}
statement_command_string = {
    "STRING"
  ~ " "
  ~ (expression | value_any)
}
// Keystroke Injection - Cursor Keys.
statement_command_key = {
  (
      statement_command_backspace
    | statement_command_delete
    | statement_command_del
    | statement_command_downarrow
    | statement_command_down
    | statement_command_end
    | statement_command_home
    | statement_command_insert
    | statement_command_leftarrow
    | statement_command_left
    | statement_command_pagedown
    | statement_command_pageup
    | statement_command_rightarrow
    | statement_command_right
    | statement_command_space
    | statement_command_tab
    | statement_command_uparrow
    | statement_command_up
    | statement_command_app
    | statement_command_break
    | statement_command_enter
    | statement_command_escape
    | statement_command_f10
    | statement_command_f11
    | statement_command_f12
    | statement_command_f0
    | statement_command_f1
    | statement_command_f2
    | statement_command_f3
    | statement_command_f4
    | statement_command_f5
    | statement_command_f6
    | statement_command_f7
    | statement_command_f8
    | statement_command_f9
    | statement_command_menu
    | statement_command_pause
    | statement_command_printscreen
    | statement_command_alt
    | statement_command_command
    | statement_command_control
    | statement_command_ctrl
    | statement_command_gui
    | statement_command_shift
    | statement_command_windows
    | statement_command_option
    | statement_command_capslock
    | statement_command_numlock
    | statement_command_scrollock
  )
  ~ whitespace*
  ~ (statement_command_key | value_any)?
}
statement_command_backspace = {
  "BACKSPACE"
}
statement_command_delete = {
  "DELETE"
}
statement_command_del = {
  "DEL"
}
statement_command_downarrow = {
  "DOWNARROW"
}
statement_command_down = {
  "DOWN"
}
statement_command_end = {
  "END"
}
statement_command_home = {
  "HOME"
}
statement_command_insert = {
  "INSERT"
}
statement_command_leftarrow = {
  "LEFTARROW"
}
statement_command_left = {
  "LEFT"
}
statement_command_pagedown = {
  "PAGEDOWN"
}
statement_command_pageup = {
  "PAGEUP"
}
statement_command_rightarrow = {
  "RIGHTARROW"
}
statement_command_right = {
  "RIGHT"
}
statement_command_space = {
  "SPACE"
}
statement_command_tab = {
  "TAB"
}
statement_command_uparrow = {
  "UPARROW"
}
statement_command_up = {
  "UP"
}
// Keystroke Injection - System Keys.
statement_command_app = {
  "APP"
}
statement_command_break = {
  "BREAK"
}
statement_command_enter = {
  "ENTER"
}
statement_command_escape = {
  "ESCAPE"
}
// TODO: Does 10 work? Are the docs missing it? https://docs.hak5.org/hak5-usb-rubber-ducky/duckyscript-tm-quick-reference#system-keys
statement_command_f10 = {
  "F10"
}
statement_command_f11 = {
  "F11"
}
statement_command_f12 = {
  "F12"
}
statement_command_f0 = {
  "F0"
}
statement_command_f1 = {
  "F1"
}
statement_command_f2 = {
  "F2"
}
statement_command_f3 = {
  "F3"
}
statement_command_f4 = {
  "F4"
}
statement_command_f5 = {
  "F5"
}
statement_command_f6 = {
  "F6"
}
statement_command_f7 = {
  "F7"
}
statement_command_f8 = {
  "F8"
}
statement_command_f9 = {
  "F9"
}
statement_command_menu = {
  "MENU"
}
statement_command_pause = {
  "PAUSE"
}
statement_command_printscreen = {
  "PRINTSCREEN"
}
// Keystroke Injection - Basic Modifier Keys.
statement_command_alt = {
  "ALT"
}
statement_command_command = {
  "COMMAND"
}
statement_command_control = {
  "CONTROL"
}
statement_command_ctrl = {
  "CTRL"
}
statement_command_gui = {
  "GUI"
}
statement_command_shift = {
  "SHIFT"
}
statement_command_windows = {
  "WINDOWS"
}
// Keystroke Injection - Advanced Modifier Keys.
statement_command_option = {
  "OPTION"
}
// Keystroke Injection - Standalone Modifier Keys.
statement_command_inject_mod = {
  "INJECT_MOD"
}
// Keystroke Injection - Lock Keys.
statement_command_capslock = {
  "CAPSLOCK"
}
statement_command_numlock = {
  "NUMLOCK"
}
// TODO: Is this SCROLLOCK or SCROLLLOCK?
statement_command_scrollock = {
  "SCROLLOCK"
}
// Button.
statement_command_wait_for_button_press = {
  "WAIT_FOR_BUTTON_PRESS"
}
statement_command_button_def = {
  "BUTTON_DEF"
}
statement_command_disable_button = {
  "DISABLE_BUTTON"
}
statement_command_enable_button = {
  "ENABLE_BUTTON"
}
// LED.
statement_command_led_off = {
  "LED_OFF"
}
statement_command_led_r = {
  "LED_R"
}
statement_command_led_g = {
  "LED_G"
}
// Attack Mode.
statement_command_save_attackmode = {
  "SAVE_ATTACKMODE"
}
statement_command_restore_attackmode = {
  "RESTORE_ATTACKMODE"
}
statement_command_attackmode = {
  "ATTACKMODE"
}
// Constants.
statement_command_define = {
    "DEFINE"
  ~ " "
  ~ keyword_name
  ~ " "
  ~ value_any
}
// Randomization.
statement_command_random_lowercase_letter = {
  "RANDOM_LOWERCASE_LETTER"    // abcdefghijklmnopqrstuvwxyz
}
statement_command_random_uppercase_letter = {
  "RANDOM_UPPERCASE_LETTER"    // ABCDEFGHIJKLMNOPQRSTUVWXYZ
}
statement_command_random_letter = {
  "RANDOM_LETTER"              // abcdefghijklmnopqrstuvwxyzABCDEFGHIJKLMNOPQRSTUVWXYZ
}
statement_command_random_number = {
  "RANDOM_NUMBER"              // 0123456789
}
statement_command_random_special = {
  "RANDOM_SPECIAL"             // !@#$%^&*()
}
statement_command_random_char = {
  "RANDOM_CHAR"                // abcdefghijklmnopqrstuvwxyz ABCDEFGHIJKLMNOPQRSTUVWXYZ0123456789!@#$%^&*()
}
// ATTACKMODE.
statement_command_vid_random = {
  "VID_RANDOM"                 // Random Vendor ID
}
statement_command_pid_random = {
  "PID_RANDOM"                 // Random Product ID
}
statement_command_man_random = {
  "MAN_RANDOM"                 // Random 32 alphanumeric character iManufacturer
}
statement_command_prod_random = {
  "PROD_RANDOM"                // Random 32 alphanumeric character iProduct
}
statement_command_serial_random = {
  "SERIAL_RANDOM"              // Random 12 digit serial number
}
// Holding Keys.
statement_command_hold = {
  "HOLD"
}
statement_command_release = {
  "RELEASE"
}
// Payload Control.
statement_command_restart_payload = {
  "RESTART_PAYLOAD"
}
statement_command_stop_payload = {
  "STOP_PAYLOAD"
}
statement_command_reset = {
  "RESET"
}
// Payload Hiding.
statement_command_hide_payload = {
  "HIDE_PAYLOAD"               // Hides the inject.bin and seed.bin files from the MicroSD card.
}
statement_command_restore_payload = {
  "RESTORE_PAYLOAD"            // Restores the inject.bin and seed.bin files to the MicroSD card.
}
// Lock Keys.
statement_command_wait_for_caps_on = {
  "WAIT_FOR_CAPS_ON"           // Pause until caps lock is turned on.
}
statement_command_wait_for_caps_off = {
  "WAIT_FOR_CAPS_OFF"          // Pause until caps lock is turned off.
}
statement_command_wait_for_caps_change = {
  "WAIT_FOR_CAPS_CHANGE"       // Pause until caps lock is toggled on or off.
}
statement_command_wait_for_num_on = {
  "WAIT_FOR_NUM_ON"            // Pause until num lock is turned on.
}
statement_command_wait_for_num_off = {
  "WAIT_FOR_NUM_OFF"           // Pause until num lock is turned off.
}
statement_command_wait_for_num_change = {
  "WAIT_FOR_NUM_CHANGE"        // Pause until num lock is toggled on or off.
}
statement_command_wait_for_scroll_on = {
  "WAIT_FOR_SCROLL_ON"         // Pause until scroll lock is turned on.
}
statement_command_wait_for_scroll_off = {
  "WAIT_FOR_SCROLL_OFF"        // Pause until scroll lock is turned off.
}
statement_command_wait_for_scroll_change = {
  "WAIT_FOR_SCROLL_CHANGE"     // Pause until scroll lock is toggled on or off.
}
// 'SAVE' & 'RESTORE' Commands.
statement_command_save_host_keyboard_lock_state = {
  "SAVE_HOST_KEYBOARD_LOCK_STATE"
}
statement_command_restore_host_keyboard_lock_state = {
  "RESTORE_HOST_KEYBOARD_LOCK_STATE"
}
// Exfiltration.
statement_command_exfil = {
    "EXFIL"
  ~ " "
  ~ variable
}
// }


// // // // // // // // // // // // // // // // // // // // // // // // // // // // // // // //
//                                      Variables                                            //
// // // // // // // // // // // // // // // // // // // // // // // // // // // // // // // //
// {
statement_variable_declaration = {
    "VAR"
  ~ whitespace*
  ~ variable
  ~ whitespace*
  ~ "="
  ~ whitespace*
  ~ expression
}

statement_variable_assignment = {
    variable
  ~ whitespace*
  ~ "="
  ~ whitespace*
  ~ expression
}

variable = _{
  "$" ~ keyword_name
}

keyword_name = {
  !ASCII_DIGIT ~ (not_special ~ (ASCII_ALPHANUMERIC | "_"))+
}

// Random Integers.
// '$_RANDOM_MIN'            : minimum random integer.
// '$_RANDOM_MAX'            : maximum random integer.
// '$_RANDOM_INT'            : random integer between '$_RANDOM_MIN' and '$_RANDOM_MAX'.
// Jitter.
// '$_JITTER_ENABLED'        : Set TRUE to start and FALSE to stop jitter.
// '$_JITTER_MAX'            : Integer (0-65535) of maximum time in milliseconds between keystrokes. Default 20.
// Variables Internal
// BUTTON
// '$_BUTTON_ENABLED'        : Returns TRUE if the button is enabled or FALSE if the button is disabled.
// '$_BUTTON_USER_DEFINED'   : Returns TRUE if a BUTTON_DEF has been implemented in the payload or FALSE if it hasn't been implemented.
// '$_BUTTON_PUSH_RECEIVED'  : Returns TRUE if the button has ever been pressed. May be retrieved or set.
// '$_BUTTON_TIMEOUT'        : The button debounce, or cooldown time before counting the next button press, in milliseconds. The default value is 1000.
// LED
// '$_SYSTEM_LEDS_ENABLED'   : Default set TRUE. May be retrieved or set. Boot and ATTACKMODE change LED.
// '$_STORAGE_LEDS_ENABLED'  : Default set TRUE. May be retrieved or set. Blinks the LED red/green on storage read/write in ATTACKMODE STORAGE.
// '$_LED_CONTINUOUS_SHOW_STORAGE_ACTIVITY' : Default set TRUE. May be retrieved or set. The LED will light solid green when the storage has been inactive for longer than $_STORAGE_ACTIVITY_TIMEOUT (default 1000 ms). Otherwise, the LED will light red when active.
// '$_INJECTING_LEDS_ENABLED' : Default set TRUE. May be retrieved or set. The LED will blink green on payload execution.
// '$_EXFIL_LEDS_ENABLED'    : Default set TRUE. May be retrieved or set. The LED will blink green during Keystroke Reflection.
// '$_LED_SHOW_CAPS'         : Toggles TRUE or FALSE based on whether the caps lock LED is set on or off by the host. May only be retrieved. Cannot be set.
// '$_LED_SHOW_NUM'          : Toggles TRUE or FALSE based on whether the num lock LED is set on or off by the host. May only be retrieved. Cannot be set.
// '$_LED_SHOW_SCROLL'       : Toggles TRUE or FALSE based on whether the num lock LED is set on or off by the host. May only be retrieved. Cannot be set.
// ATTACKMODE
// '$_CURRENT_VID'           : Returns the currently operating Vendor ID with endian swapped. May only be retrieved. Cannot be set.
// '$_CURRENT_PID'           : Returns the currently operating Product ID with endian swapped. May only be retrieved. Cannot be set.
// '$_CURRENT_ATTACKMODE'    : Returns the currently operating ATTACKMODE represented as 0 for OFF, 1 for HID, 2 for STORAGE and 3 for both HID and STORAGE
// RANDOM
// '$_RANDOM_INT'            : Random integer within set range.
// '$_RANDOM_MIN'            : Random integer minimum range (unsigned, 0-65535)
// '$_RANDOM_MAX'            : Random integer maximum range (unsigned, 0-65535)
// '$_RANDOM_SEED'           : Random seed from seed.bin
// JITTER
// '$_JITTER_ENABLED'        : Set TRUE to enable jitter. Default FALSE.
// '$_JITTER_MAX'            : Sets the maximum time between key presses in milliseconds. The default maximum is 20 ms.
// LOCK KEYS
// '$_CAPSLOCK_ON'           : TRUE if on, FALSE if off.
// '$_NUMLOCK_ON'            : TRUE if on, FALSE if off.
// '$_SCROLLLOCK_ON'         : TRUE if on, FALSE if off.
// '$_SAVED_CAPSLOCK_ON'     : On USB attach, sets TRUE or FALSE depending on the reported OS condition.
// '$_SAVED_NUMLOCK_ON'      : On USB attach, sets TRUE or FALSE depending on the reported OS condition.
// '$_SAVED_SCROLLLOCK_ON'   : On USB attach, sets TRUE or FALSE depending on the reported OS condition.
// '$_RECEIVED_HOST_LOCK_LED_REPLY' : On receipt of any lock state LED control code, sets TRUE. This flag is helpful for fingerprinting certain operating systems (e.g. macOS) or systems which do not communicate lock keys "correctly".
// STORAGE
// '$_STORAGE_ACTIVITY_TIMEOUT' : As payload is running, this value decrements if storage activity is not detected. Default value is 1000.
// EXFILTRATION
// '$_EXFIL_MODE_ENABLED'    : Set TRUE to enable Keystroke Reflection. Will listen for caps and num lock changes, writing binary values to loot.bin. Default FALSE. num=1, caps=0.
// OS_DETECT
// '$_HOST_CONFIGURATION_REQUEST_COUNT' : Used by OS_DETECT extension to detect device enumeration count.
// '$_OS'                    : Used by OS_DETECT extension to return value of fingerprinted operating system. May return WINDOWS, MACOS, LINUX, CHROMEOS, ANDROID, IOS. These names are reserved and should not be used in user variables.
// }


// // // // // // // // // // // // // // // // // // // // // // // // // // // // // // // //
//                                    Control Flow                                           //
// // // // // // // // // // // // // // // // // // // // // // // // // // // // // // // //
// {
statement_block_if = {
    "IF"
  ~ whitespace+
  ~ expression
  ~ whitespace+
  ~ "THEN"
  ~ NEWLINE
  ~ statement_block_if_case_true
  ~ (
      PEEK_ALL
    ~ "ELSE"
    ~ NEWLINE
    ~ statement_block_if_case_false
  )?
  ~ (
      PEEK_ALL
    ~ "END_FI"
  )
}

statement_block_while = {
    "WHILE"
  ~ whitespace+
  ~ expression
  ~ NEWLINE
  ~ control_block_list_statement*
  ~ (
      PEEK_ALL
    ~ "END_WHILE"
  )
}

statement_block_if_case_true = {
  control_block_list_statement*
}

statement_block_if_case_false = {
  control_block_list_statement*
}

control_block_list_statement = _{
    PEEK_ALL
  ~ PUSH(indentation)
  ~ (NEWLINE | (statement ~ NEWLINE))
  ~ DROP
}
// }


// // // // // // // // // // // // // // // // // // // // // // // // // // // // // // // //
//                                         Data                                              //
// // // // // // // // // // // // // // // // // // // // // // // // // // // // // // // //
// {
expression = {
    expression_negation*
  ~ (variable | value)
  ~ (
        whitespace+
      ~ operator
      ~ whitespace+
      ~ expression
  )?
}

expression_negation = {
  "NOT" ~ whitespace+
}

value = {
    value_number
  | value_boolean
  | value_string
  | "(" ~ expression ~ ")"
}

value_number = {
  ASCII_DIGIT+
}

value_boolean = {
  "TRUE" | "FALSE"
}

value_string = _{ "\"" ~ (value_string_value | value_string_escape)* ~ "\"" }
value_string_value = { (!("\\" | "\"") ~ ANY)+ }
value_string_hex = _{ '0'..'9' | 'a'..'f' | 'A'..'F' }
value_string_unicode_hex = { value_string_hex{1, 6} }
value_string_escape_predefined = { "n" | "r" | "t" | "\\" | "0" | "\"" | "'" }
value_string_byte = { "x" ~ value_string_hex{2} }
value_string_unicode = { "u" ~ "{" ~ value_string_unicode_hex ~ "}" }
value_string_escape = { "\\" ~ (value_string_escape_predefined | value_string_byte | value_string_unicode) }

value_any = {
  (!NEWLINE ~ ANY)*
}

operator = {
    operator_comparator
  | operator_logical
}

operator_comparator = {
    "=="                  // Equal to.
  | "!="                  // Not equal to.
  | ">="                  // Greater than.
  | ">"                   // Less than.
  | "<="                  // Greater than or equal to.
  | "<"                   // Less than or equal to.
}

operator_logical = {
    "&&"                  // Logical AND. If both the operands are non-zero, the condition is TRUE.
  | "||"                  // Logical OR. If any of the two operands is non-zero, the condition is TRUE.
}

not_special = _{
    !NEWLINE
  ~ !")"
  ~ !"("
  ~ !"["
  ~ !"]"
  ~ !">"
  ~ !"<"
  ~ !"{"
  ~ !"}"
  ~ !"="
  ~ !"!"
  ~ !">"
  ~ !"<"
  ~ !"&"
  ~ !"|"
  ~ !whitespace
}

indentation = _{
  (" "{2} | "\t")
}

whitespace = _{
  (" " | "\t")
}

// TODO:
// Bitwise Operators.
// '&'  : Bitwise AND. If the corresponding bits of the two operands is 1, will result in 1. Otherwise if either bit of an operand is 0, the result of the corresponding bit is evaluated as 0.
// '|'  : Bitwise OR. If at least one corresponding bit of the two operands is 1, will result in 1.
// '>>' : Right Shift. Accepts two numbers. Right shifts the bits of the first operand. The second operand determines the number of places to shift.
// '<<' : Left Shift. Accepts two numbers. Left shifts the bits of the first operand. The second operand decides the number of places to shift.

// TODO:
// Conditional Statements.
// Functions.
// 'FUNCTION'
// 'RETURN'

// }
